apply plugin: 'java'

defaultTasks 'jointBuild'

ext.groovyCheckoutDir = '../groovy_checkout'
ext.githubCheckoutDir = 'githubCheckout'
ext.grailsCheckoutDir = "${githubCheckoutDir}/grails"

task buildGroovy(type: GradleBuild) {
    dir = groovyCheckoutDir
    startParameter.systemPropertiesArgs = ['skipExamples': 'true', 'skipTests': 'true']
    tasks = ['clean', 'jarAll']
}

task testGrails(type: GradleBuild){
    ext.groovyJarPath = new File(groovyCheckoutDir, 'target/libs/groovy-all-2.3.1-SNAPSHOT.jar')
    dir = grailsCheckoutDir
    startParameter.systemPropertiesArgs = ['groovy.jar': groovyJarPath.absolutePath, 'groovy.grails.joint': 'true']
    tasks = ['clean', 'install', 'test']
    startParameter.excludedTaskNames = ['javadoc']
}

task getGrails << {
    ant {
        mkdir dir: githubCheckoutDir
        get src:"http://github.com/grails/grails-core/archive/groovy_2.3_jdk8.zip", dest:"${githubCheckoutDir}/grails-core-src.zip", verbose:"true"
        unzip(src: "${githubCheckoutDir}/grails-core-src.zip", dest: githubCheckoutDir) {
            mapper type: 'regexp', from:/(grails-core-groovy_2.3_jdk8[\w]*\/)(.*)/, to:/grails\/\2/
        }
    }
}

clean {
    delete githubCheckoutDir
}

task jointBuild(dependsOn: ['getGrails', 'buildGroovy', 'testGrails'])

