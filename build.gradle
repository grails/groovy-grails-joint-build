apply plugin: 'java'

defaultTasks 'jointBuild'

ext.groovyCheckoutDir = '../groovy_checkout'
ext.githubCheckoutDir = 'githubCheckout'
ext.grailsCheckoutDir = "${githubCheckoutDir}/grails"

task buildGroovy(type: Exec) {
    ext.tasks = ['clean', 'jarAll']
    workingDir = groovyCheckoutDir
    commandLine = ['./gradlew', *tasks, '-DskipExamples=true', '-DskipTests=true']
}

task testGrails(type: GradleBuild){
    if(project.hasProperty('groovy.version')) {
        ext.groovyVersion = project.getProperty('groovy.version')
    } else {
        ext.groovyVersion = '2.3.5-SNAPSHOT'
    }
    ext.groovyJarPath = new File(groovyCheckoutDir, "target/libs/groovy-all-${groovyVersion}.jar")
    dir = grailsCheckoutDir
    startParameter.systemPropertiesArgs = ['groovy.jar': groovyJarPath.absolutePath, 'groovy.grails.joint': 'true']
    tasks = ['clean', 'populateDependencies', 'test']
}

task getGrails << {
    ant {
        mkdir dir: githubCheckoutDir
        get src:"http://github.com/grails/grails-core/zipball/2.5.x", dest:"${githubCheckoutDir}/grails-core-src.zip", verbose:"true"
        unzip(src: "${githubCheckoutDir}/grails-core-src.zip", dest: githubCheckoutDir) {
            mapper type: 'regexp', from:/(grails-core-[\w]*\/)(.*)/, to:/grails\/\2/
        }
    }
}

clean {
    delete githubCheckoutDir
}

task jointBuild(dependsOn: ['getGrails', 'buildGroovy', 'testGrails'])

